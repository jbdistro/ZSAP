REPORT z_role_user_trans.

TABLES: agr_define, agr_users, agr_tcodes, tstct.

TYPES: BEGIN OF ty_output,
         usuario TYPE xubname,
         rol     TYPE agr_define-agr_name,
         trans   TYPE tcode,
         descr   TYPE tstct-ttext,
       END OF ty_output.

DATA: gt_output TYPE TABLE OF ty_output,
      lo_alv    TYPE REF TO cl_salv_table.

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.
SELECT-OPTIONS: s_user FOR agr_users-uname,
                s_role FOR agr_define-agr_name,
                s_tcode FOR agr_tcodes-tcode.
SELECTION-SCREEN END OF BLOCK b1.

START-OF-SELECTION.
  SELECT r~uname AS usuario,
         a~agr_name AS rol,
         t~tcode AS trans,
         s~ttext AS descr
    FROM agr_users AS r
    INNER JOIN agr_define AS a ON r~agr_name = a~agr_name
    INNER JOIN agr_tcodes AS t ON a~agr_name = t~agr_name
    LEFT JOIN tstct AS s ON s~sprsl = @sy-langu AND s~tcode = t~tcode
    WHERE r~uname IN @s_user
      AND a~agr_name IN @s_role
      AND t~tcode IN @s_tcode
    INTO TABLE @gt_output.

  IF sy-subrc <> 0.
    MESSAGE 'No se encontraron datos' TYPE 'I'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  cl_salv_table=>factory( IMPORTING r_salv_table = lo_alv
                          CHANGING  t_table      = gt_output ).
  lo_alv->display( ).
